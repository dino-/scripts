#!/usr/bin/perl

#--------------------------------------------------------------------------
# $HeadURL: file:///var/lib/svn/scripts/quizspoil $
#--------------------------------------------------------------------------
# $Revision: 157 $
# $Date: 2005-09-12 15:41:03 -0400 (Mon, 12 Sep 2005) $
# $Author: dmorelli $
#
# Perl qotw spoiler calculator
#--------------------------------------------------------------------------

use Time::Local 'timegm';


my $date_field;

while (<>) {
	chomp;
	last unless /\S/;
	if (s/^Date:\s+//) {
		$date_field = $_;
		while (<>) {                # read continuation lines?
			last unless s/^\s//;
			chomp;
			$date_field .= $_;
		}
	last;
	}
}

die "No Date: field found\n" unless defined $date_field;

# Typical value:
# Wed, 30 Oct 2002 21:34:54 -0000
my ($dy, $mo, $yr, $hr, $mn, $sc, $tzd, $tzh, $tzm) =
	$date_field =~ 
		/\w\w\w,\              #  Day of week
			(\d\d)\ (\w\w\w)\ (\d\d\d\d)\ # Day, month, year
			(\d\d):(\d\d):(\d\d)\ # Time
			([+-])(\d\d)(\d\d)/x; # Time zone

unless (defined $dy) {
	die "Couldn't parse Date: field\n";
}

my %mo = qw(jan 0 feb 1 mar 2 apr 3 may 4 jun 5
	jul 6 aug 7 sep 8 oct 9 nov 10 dec 11);

die "Unknown month name '$mo'\n" unless exists $mo{lc $mo};
my $msgtime = timegm($sc, $mn, $hr, $dy, $mo{lc $mo}, $yr-1900);
my $tz_adjust = ($tzm * 60 + $tzh * 3600);
$tz_adjust *= -1 if $tzd eq '+';
$msgtime += $tz_adjust;   # msgtime is now adjusted for time zone

my $time_left = ($msgtime + 60 * 3600) - time();

if ($time_left < 0) {
	print "It is okay to send spoilers for this quiz\n";
} else {
	print "It is too soon to send spoilers for this quiz.\n";
	my $hr = int($time_left / 3600);
	my $min = int(($time_left - 3600*$hr)/60 + 0.5);
	my $hours = ($hr == 1 ? 'hour' : 'hours');
	my $minutes = ($min == 1 ? 'minute' : 'minutes');
	print "You may send spoilers in another $hr $hours $min $minutes.\n";
}


__END__

From mjd@plover.com Thu Aug  5 23:21:26 2004
Date: Thu, 05 Aug 2004 22:27:45 -0400
From: Mark Jason Dominus <mjd@plover.com>
To: perl-qotw-discuss@plover.com
Subject: When is it OK to send spoilers?


In the past few weeks there has been both doubt and confusion about
whether and when it was OK to send hints, solutions, and spoilers for
quizzes.

An early quiz (#3, in fact) was to write a program to solve that
problem.  Here's a sample solution to that quiz.   To use it, 
give it the current quiz email message on standard input.

I hope people find this helpful.
