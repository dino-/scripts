#! /usr/bin/perl -w

#--------------------------------------------------------------------------
# $HeadURL: file:///var/lib/svn/scripts/trunk/cdburn $
#--------------------------------------------------------------------------
# $Revision: 156 $
# $Date: 2005-09-12 14:00:39 -0400 (Mon, 12 Sep 2005) $
# $Author: dmorelli $
#
# Script to burn iso images to CD
#--------------------------------------------------------------------------

use strict;
use File::Basename;
use Getopt::Long;


my @commands;
my $basename = basename $0;
my %opts = (
    device => '/dev/hdc',
    speed => 24,
);

#--------------------------------------------------------------------------
sub displayUsage {
    print <<USAGE;
$basename - Burn a data or music CD
version 1.0.3  Dino Morelli  dmorelli\@reactorweb.net

usage:
   $basename [-eksv] [-d device] [-x speed] file
   $basename --help

switches:
   -e, --eject          Eject when finished, default: false
   -d, --device=DEVICE  SCSI device, default: $opts{device}
                        If you need to know more, like SCSI device numbers, 
                        drive model info, etc:
                        `cdrecord dev=$opts{device} -scanbus`
   -h, --help           This information.
   -k, --check          Check disk after burn, default: false
                        This is only valid for ISO and will ignore -e
   -s, --simulate       Simulate, don't really burn, default: false
   -v, --verbose        Verbose, default: false
   -x, --speed=SPEED    Burn speed, default: $opts{speed}
   file                 ISO or TOC file to burn (.iso or .toc)
USAGE

	exit;
}


#--------------------------------------------------------------------------
# It all starts here

# Parse arguments
Getopt::Long::Configure ("bundling");
GetOptions(
    \%opts,
    'eject|e',
    'device|d=s',
    'help|h',
    'check|k',
    'simulate|s',
    'verbose|v',
    'speed|x=i',
) or displayUsage;

displayUsage if $opts{help};

my $file = shift;
if(!defined $file) {
    print "No file specified.\n";
    displayUsage;
}

# Figure out what type of file we have and set up the commands
SWITCH: {
	$file =~ m|.*\.iso| && do {
		my $command = 
            "cdrecord driveropts=burnfree -tao dev=$opts{device} ";
		if(!$opts{check})
		{ $command .= ($opts{eject} ? "-eject " : ""); }
		$command .= ($opts{simulate} ? "-dummy " : "");
		$command .= "speed=$opts{speed} ";
		$command .= ($opts{verbose} ? "-v " : "");
		$command .= $file;
		push(@commands, $command);

		if($opts{check}) {
            push(@commands, "sleep 2");
			push(@commands, "mount /mnt/cdrom");
			push(@commands, "ls -l /mnt/cdrom/");
			push(@commands, "umount /mnt/cdrom");
		}

		last SWITCH;
	};
	$file =~ m|.*\.toc| && do {
		if($opts{check}) { print "** Note: -k switch not possible for CDDA\n"; }

		my $command = "cdrdao ";
		$command .= ($opts{simulate} ? "simulate " : "write ");
		$command .= "--device $opts{device} ";
		$command .= "--speed $opts{speed} ";
		$command .= ($opts{eject} ? "--eject " : "");
		$command .= ($opts{verbose} ? "-v 1 " : "");
		$command .= $file;
		push(@commands, $command);

		last SWITCH;
	};
}

# Perform the commands
foreach (@commands) {
	print "$basename executing command: $_\n";
	system $_;
}
