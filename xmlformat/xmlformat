#! /usr/bin/perl -w

#--------------------------------------------------------------------------
# $HeadURL: file:///var/lib/svn/scripts/xmlformat/xmlformat $
#--------------------------------------------------------------------------
# $Revision: 165 $
# $Date: 2005-11-20 22:40:26 -0500 (Sun, 20 Nov 2005) $
# $Author: dmorelli $
#
# Pretty print XML documents.
#--------------------------------------------------------------------------

use strict;
use File::Basename;
use Getopt::Std;
use IO::File;
use XML::Handler::YAWriter;
use XML::Parser::PerlSAX;


our ($opt_c, $opt_p, $opt_a, $opt_s);
my $basename = basename $0;
my $usage=<<EOF;
$basename - XML formatter  v1.0.1

usage:
    $basename OPTIONS XMLFILE

options:
   -c   Remove all comments
   -p   Pretty print the document
   -a   When pretty printing, put attrs on own line
   -s   Strip all meaningless whitespace
EOF

# Parse args
getopts('cpas');
my $inFile = shift;

# Bad args, show help and quit
unless ($inFile) { die "$usage\n"; }

# Construct the options hashes
my %options = (
	Output => new IO::File ( ">-" ),
	Pretty => {
		CatchEmptyElement => 1,
		CompactAttrIndent => 1,
	}
);
if($opt_c) {
	$options{Pretty}{NoComments} = 1;
}
if($opt_p) {
	$options{Pretty}{PrettyWhiteIndent} = 1;
	$options{Pretty}{PrettyWhiteNewline} = 1;
}
if($opt_a) {
	$options{Pretty}{CompactAttrIndent} = 0;
}
if($opt_s) {
	$options{Pretty}{NoWhiteSpace} = 1;
}

# Construct a handler
my $handler = new XML::Handler::YAWriter(%options);

# Construct a parser and do it
my $parser = XML::Parser::PerlSAX->new(
	Handler => $handler,
	Source => { ByteStream => new IO::File($inFile) }
);
$parser->parse();
