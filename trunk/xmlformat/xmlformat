#! /usr/bin/perl -w

#--------------------------------------------------------------------------
# $RCSfile$
#--------------------------------------------------------------------------
# $Revision: 61 $
# $Date: 2004-12-15 14:43:45 -0500 (Wed, 15 Dec 2004) $
# $Author: dmorelli $
#
# Pretty print XML documents.
#--------------------------------------------------------------------------

use strict;
use Getopt::Std;
use IO::File;
use XML::Handler::YAWriter;
use XML::Parser::PerlSAX;


our ($opt_c, $opt_p, $opt_a, $opt_s);
my $usage=<<EOF;
$0 - XML formatter  v1.0.1
  usage: $0 [-c] [-p|s] xmlFile

  -c        Remove all comments
  -p        Pretty print the document
  -a        When pretty printing, put attrs on own line
  -s        Strip all meaningless whitespace
  xmlFile   XML document to format
EOF

# Parse args
getopts('cpas');
my $inFile = shift;

# Bad args, show help and quit
unless ($inFile) { die "$usage\n"; }

# Construct the options hashes
my %options = (
	Output => new IO::File ( ">-" ),
	Pretty => {
		CatchEmptyElement => 1,
		CompactAttrIndent => 1,
	}
);
if($opt_c) {
	$options{Pretty}{NoComments} = 1;
}
if($opt_p) {
	$options{Pretty}{PrettyWhiteIndent} = 1;
	$options{Pretty}{PrettyWhiteNewline} = 1;
}
if($opt_a) {
	$options{Pretty}{CompactAttrIndent} = 0;
}
if($opt_s) {
	$options{Pretty}{NoWhiteSpace} = 1;
}

# Construct a handler
my $handler = new XML::Handler::YAWriter(%options);

# Construct a parser and do it
my $parser = XML::Parser::PerlSAX->new(
	Handler => $handler,
	Source => { ByteStream => new IO::File($inFile) }
);
$parser->parse();
